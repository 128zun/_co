// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         
        instruction[16], 
        reset;           

    OUT outM[16],        
        writeM,          
        addressM[15],    
        pc[15];          

    PARTS:
    Not(in=instruction[15], out=isA);
    Not(in=isA, out=isC);

    And(a=isC, b=instruction[5], out=isLoadA);
    Or(a=isA, b=isLoadA, out=loadA);

    Mux16(a=instruction, b=aluOut, sel=isLoadA, out=inA);
    ARegister(in=inA, load=loadA, out=outA, out[0..14]=addressM);

    Mux16(a=outA, b=inM, sel=instruction[12], out=outAM);

    And(a=isC, b=instruction[4], out=loadD);
    DRegister(in=aluOut, load=loadD, out=outD);

    ALU(x=outD, y=outAM, 
        zx=instruction[11], nx=instruction[10], 
        zy=instruction[9], ny=instruction[8], 
        f=instruction[7], no=instruction[6], 
        out=aluOut, out=outM, zr=zr, ng=ng);

    And(a=isC, b=instruction[3], out=writeM);

    Or(a=zr, b=ng, out=notPos);
    Not(in=notPos, out=pos);

    And(a=instruction[0], b=pos, out=j3);
    And(a=instruction[1], b=zr, out=j2); 
    And(a=instruction[2], b=ng, out=j1);

    Or(a=j1, b=j2, out=j12);
    Or(a=j12, b=j3, out=jumpCondition);

    And(a=isC, b=jumpCondition, out=doJump);
    
    Not(in=doJump, out=doInc);

    PC(in=outA, load=doJump, inc=doInc, reset=reset, out[0..14]=pc);
}